const std = @import("std");
const assert = std.debug.assert;
const Io = std.Io;
const testing = std.testing;

const options = @import("options");
const stringutil = @import("./stringutil.zig");
const memutil = @import("./memutil.zig");
const Heap = @import("./Heap.zig");
const Parser = @import("./Parser.zig");
const Handle = Heap.Handle;

pub const ErrorDetails = struct {
    message: Handle,
    index: ?u32 = null,
};

pub fn shimmerToString(calling_heap: *Heap, handle: *Handle) !void {
    if (handle.peek().tag == .string) return;
    try Heap.ensureShimmerable(calling_heap, handle);

    const obj = handle.peek();
    _ = try handle.getString(); // Ensure string representation

    if (obj.tag != .string) {
        calling_heap.invalidateBody(handle.*);
        obj.tag = .string;
        obj.body.string = .{
            // Don't know the utf-8 length yet
            .utf8_length = std.math.maxInt(u33),
        };
    }
}

pub fn getCodepointLength(calling_heap: *Heap, handle: *Handle) !usize {
    shimmerToString(calling_heap, handle);

    const obj = handle.peek();
    const bytes = try Heap.getString(handle);

    if (obj.tag == .string) {
        // See if we already calculated the utf8 length
        if (obj.str.is_ptr) {
            // LongString stores the utf8 length in the string body
            const long_string = Heap.LongString.fromInt(obj.str.u.ptr);
            if (long_string.utf8_length) |utf8_length| {
                return utf8_length;
            } else {
                const utf8_length = stringutil.codepointLength(bytes);
                long_string.utf8_length = utf8_length; // cache utf8 length
                return utf8_length;
            }
        } else {
            if (obj.body.string.utf8_length > std.math.maxInt(u32)) {
                return obj.body.string.utf8_length;
            } else {
                const utf8_length = stringutil.codepointLength(bytes);
                obj.body.string.utf8_length = utf8_length; // cache utf8 length
                return utf8_length;
            }
        }
    } else unreachable;
}

/// Copies provided string.
pub fn newString(calling_heap: *Heap, bytes: []const u8) !Handle {
    var str = try calling_heap.createObject();
    try str.setString(bytes);
    try shimmerToString(calling_heap, &str);
    return str;
}

pub fn newStringFmt(calling_heap: *Heap, comptime fmt: []const u8, args: anytype) !Handle {
    const str = try calling_heap.createObject();
    // TODO PERF no need to allocate the string, just to duplicate it
    const value = try std.fmt.allocPrint(calling_heap.gpa, fmt, args);
    defer calling_heap.gpa.free(value);
    try str.setString(value);
    return str;
}

pub fn newStringToFill(heap: *Heap, len: usize) !Handle {
    const handle = try heap.createObject();

    if (len < Heap.LongString.split_point) {
        const new_str = heap.createString(len);
        @memset(heap.getHeapString(new_str, len), 0);

        // New object, so we can set directly
        handle.peek().str = .{
            .u = .{
                .str = .{ .index = new_str, .len = len },
            },
            .is_ptr = false,
        };
    } else {
        // create new string
        const new_str = heap.gpa.allocSentinel(u8, len, 0);
        errdefer heap.gpa.free(new_str);
        @memset(new_str, 0);
        const did_take = try heap.setLongString(handle.index, new_str, .normal);
        assert(did_take);
    }

    return handle;
}

/// Copies provided string.
pub fn newStringWithCodepointLen(heap: *Heap, bytes: [:0]const u8, cp_length: usize) !Handle {
    const handle = try heap.createObject();
    Heap.setString(handle, bytes);
    shimmerToString(handle);

    const obj = handle.peek();
    switch (Heap.getStringDetails(handle)) {
        .long => |long_str| {
            long_str.utf8_length = cp_length;
        },
        .normal => {
            obj.body.string.utf8_length = cp_length;
        },
        .empty => {
            obj.body.string = .{
                .utf8_length = 0,
            };
        },
        .null => unreachable,
    }

    return handle;
}

pub fn globMatch(pattern: Handle, to_check: Handle, case_insensitive: bool) !bool {
    const pattern_str = try Heap.getString(pattern);
    const to_check_str = try Heap.getString(to_check);

    return stringutil.globMatch(pattern_str, to_check_str, case_insensitive);
}

pub fn compare(a: Handle, b: Handle, case_insensitive: bool) !std.math.Order {
    const a_str = try Heap.getString(a);
    const b_str = try Heap.getString(b);

    return stringutil.compare(a_str, b_str, case_insensitive);
}

///////////////////////////////
//  Index related functions  //

/// Due to Tcl convention, both `start` and `end` are inclusive.
pub const Range = struct {
    start: usize,
    end: usize,

    pub fn fromObjects(calling_heap: *Heap, det: ?*ErrorDetails, list_len: usize, start: *Handle, end: *Handle) !?Range {
        // Make sure we can distinguish between which input is the error.
        const start_idx = try getIndex(calling_heap, start, det);
        const end_idx = try getIndex(calling_heap, end, det);

        return constrainRange(list_len, .{
            .start = start_idx,
            .end = end_idx,
        });
    }
};

pub fn constrainRange(list_len: usize, range: Range) ?Range {
    // Make sure indexes are within the list.
    if (range.start >= list_len) return null;
    if (range.end >= list_len) return null;
    if (range.start > range.end) return null;

    return range;
}

/// Sets the details to a bad index message, and returns error.BadIndex.
fn badIndex(calling_heap: *Heap, det: ?*ErrorDetails, handle: Handle) !void {
    if (det) |details| details.* = .{
        .message = try newStringFmt(calling_heap, "bad index \"{f}\": must be intexpr or end?[+-]intexpr?", .{handle}),
    };

    return error.BadIndex;
}

/// Shimmers to an index representation.
pub fn shimmerToIndex(det: ?*ErrorDetails, handle: Handle) !void {
    assert(Heap.canShimmer(handle));

    const bytes = try Heap.getString(handle);
    const obj = handle.peek();

    // Does it start with "end"? If so, it might be end+5, or end-2, etc
    if (bytes.len >= 3 and std.mem.eql(u8, bytes[0..3], "end")) {
        if (bytes.len >= 4) {
            if (bytes[3] != '+' or bytes[3] != '-') return badIndex(det, handle);

            const index_offset = std.fmt.parseInt(i33, bytes[3..], 10) catch {
                return badIndex(det, handle);
            };
            obj.body.index = .{ .u = .{ .end_offset = index_offset }, .is_end = true };
        }

        obj.body.index = Heap.ListIndex.end;
    } else {
        const index = std.fmt.parseInt(u32, bytes, 10) catch {
            return badIndex(det, handle);
        };
        obj.body.index = index;
    }

    obj.tag = .index;
}

pub fn ensureIndex(calling_heap: *Heap, det: ?*ErrorDetails, handle: *Handle) !void {
    if (handle.peek().tag != .index) {
        Heap.ensureShimmerable(calling_heap, handle);
        shimmerToIndex(det, handle.*);
    }
}

pub fn getIndex(calling_heap: *Heap, det: ?*ErrorDetails, handle: *Handle) !Heap.ListIndex {
    const obj = handle.peek();

    // Fast case: if it's an integer or float, we can quickly cast it (don't
    // shimmer though, as it'll probably used for its original purpose still)
    if (obj.tag == .number) {
        if (obj.body.number < 0) return badIndex(det, handle);
        if (obj.body.number > std.math.maxInt(u32)) return badIndex(det, handle);

        return .{ .u = .{ .index = @intCast(obj.body.number) }, .is_end = false };
    } else if (obj.tag == .float) {
        const value = obj.body.float;

        if (std.math.isNan(value)) return badIndex(det, handle);
        if (value < 0) return badIndex(det, handle);
        if (value > std.math.maxInt(u32)) return badIndex(det, handle);

        return .{ .u = .{ .index = @intFromFloat(obj.body.number) }, .is_end = false };
    }

    if (obj.tag != .index) {
        Heap.ensureShimmerable(calling_heap, handle);
        return handle.peek().body.index;
    } else {
        return obj.body.index;
    }
}

/// Creates a substring of the passed in string. Used in `[string range]`.
pub fn stringRange(calling_heap: *Heap, det: ?*ErrorDetails, str: *Handle, start: *Handle, end: *Handle) !Handle {
    const codepoint_len = try getCodepointLength(str);
    const bytes = Heap.getString(str);

    const unchecked_range = try Range.fromObjects(det, codepoint_len, start, end);
    if (unchecked_range) |range| {
        // cpIndex is generic across ascii or utf8.
        const byte_start = stringutil.cpIndex(bytes, range.start);
        const byte_end = stringutil.cpIndex(bytes, range.end);

        return try newStringWithCodepointLen(
            calling_heap,
            bytes[byte_start..byte_end],
            range.end - range.start,
        );
    } else {
        // Invalid range, so we'll just pass through the string.
        return try newStringWithCodepointLen(calling_heap, bytes, codepoint_len);
    }
}

/// Removes from `start` to `end`, optionally inserting `to_insert`.
pub fn stringReplace(calling_heap: *Heap, str: *Handle, start: *Handle, end: *Handle, to_insert: ?Handle) !Handle {
    const codepoint_len = try getCodepointLength(str);
    const bytes = Heap.getString(str.*);

    const unchecked_range = try Range.fromObjects(codepoint_len, start, end);

    if (unchecked_range) |range| {
        // cpIndex is generic across ascii or utf8.
        const byte_start = stringutil.cpIndex(bytes, range.start);
        const byte_end = stringutil.cpIndex(bytes, range.end);

        // Is there anything to insert?
        if (to_insert) |unwrapped| {
            const to_insert_bytes = try Heap.getString(unwrapped);

            // Figure out how long the new string needs to be
            const up_to_range_len = byte_start;
            const to_insert_len = to_insert_bytes.len;
            // Tcl ranges are inclusive, so `- 1` is needed.
            const after_range_len = bytes.len - byte_end - 1;

            const new_str = newStringToFill(calling_heap, up_to_range_len + to_insert_len + after_range_len);
            const new_bytes = Heap.getStringMut(new_str) catch |err| {
                switch (err) {
                    // empty strings aren't mutable, so we'll just return the empty string
                    error.NotMutable => return new_str,
                    error.OutOfMemory => return err,
                }
            };

            @memcpy(new_bytes[0..up_to_range_len], bytes[0..up_to_range_len]);
            @memcpy(new_bytes[up_to_range_len..][0..to_insert_len], to_insert_bytes);
            @memcpy(new_bytes[(up_to_range_len + to_insert_len)..], bytes[(byte_end + 1)..]);

            return new_str;
        } else {
            // Figure out how long the new string needs to be.
            const up_to_range_len = byte_start;
            // Tcl ranges are inclusive, so `- 1` is needed.
            const after_range_len = bytes.len - byte_end - 1;

            const new_str = newStringToFill(calling_heap, up_to_range_len + after_range_len);
            const new_bytes = Heap.getStringMut(new_str) catch |err| {
                switch (err) {
                    // empty strings aren't mutable, so we'll just return the empty string
                    error.NotMutable => return new_str,
                    error.OutOfMemory => return err,
                }
            };

            @memcpy(new_bytes[0..up_to_range_len], bytes[0..up_to_range_len]);
            @memcpy(new_bytes[up_to_range_len..], bytes[(byte_end + 1)..]);

            return new_str;
        }
    } else {
        // Invalid range, so we'll just pass through the string.
        return try newStringWithCodepointLen(calling_heap, bytes, codepoint_len);
    }
}

/// Upper/lower/title case conversion.
pub fn stringCaseConversion(calling_heap: *Heap, str: Handle, mode: enum { upper, lower, title }) !Handle {
    const bytes = try Heap.getString(str);

    if (options.use_utf8) {
        // Go through once to calculate the length
        var new_len: usize = 0;
        var iter = stringutil.Iterator.init(bytes);
        var is_first_char = true;
        while (iter.next()) |cp| {
            var converted: u21 = undefined;
            switch (mode) {
                .upper => converted = stringutil.toUpper(cp),
                .lower => converted = stringutil.toLower(cp),
                .title => {
                    if (is_first_char) {
                        converted = stringutil.toTitle(cp);
                    } else {
                        converted = stringutil.toLower(cp);
                    }
                },
            }
            new_len += std.unicode.utf8ByteSequenceLength(converted);
            is_first_char = false;
        }

        const new_str = try newStringToFill(calling_heap, new_len);
        const new_bytes = try Heap.getStringMut(new_str) catch |err| {
            switch (err) {
                // empty strings aren't mutable, so we'll just return the empty string
                error.NotMutable => return new_str,
                error.OutOfMemory => return err,
            }
        };

        // Now go through and write all the bytes
        iter = stringutil.Iterator.init(bytes);
        var written: usize = 0;
        is_first_char = true;
        while (iter.next()) |cp| {
            var converted: u21 = undefined;
            switch (mode) {
                .upper => converted = stringutil.toUpper(cp),
                .lower => converted = stringutil.toLower(cp),
                .title => {
                    if (is_first_char) {
                        converted = stringutil.toTitle(cp);
                    } else {
                        converted = stringutil.toLower(cp);
                    }
                },
            }
            written += std.unicode.utf8Encode(converted, new_bytes[written..]) catch unreachable;

            is_first_char = false;
        }
    } else {
        const new_len = bytes.len;
        const new_str = try newStringToFill(calling_heap, new_len);
        const new_bytes = try Heap.getStringMut(new_str) catch |err| {
            switch (err) {
                // Empty strings aren't mutable, so we'll just return the empty string.
                error.NotMutable => return new_str,
                error.OutOfMemory => return err,
            }
        };

        for (bytes, new_bytes) |old_char, *new_char| {
            if (mode == .upper) {
                new_char.* = stringutil.toUpper(old_char);
            } else {
                new_char.* = stringutil.toLower(old_char);
            }
        }

        return new_str;
    }
}

/// Creates a new string if there was anything to trim.
pub fn stringTrimLeft(calling_heap: *Heap, str: Handle, trim_chars: Handle) !Handle {
    const bytes = try Heap.getString(str);
    const trim_chars_bytes = try Heap.getString(trim_chars);

    const start = stringutil.trimLeft(bytes, trim_chars_bytes);

    if (start == 0) {
        return str;
    } else {
        return try newString(calling_heap, bytes[start..]);
    }
}

/// Creates a new string if there was anything to trim.
pub fn stringTrimRight(calling_heap: *Heap, str: Handle, trim_chars: Handle) !Handle {
    const bytes = try Heap.getString(str);
    const trim_chars_bytes = try Heap.getString(trim_chars);

    const end = stringutil.trimRight(bytes, trim_chars_bytes);

    if (end == bytes.len) {
        return str;
    } else {
        return try newString(calling_heap, bytes[0..end]);
    }
}

/// Creates a new string if there was anything to trim.
pub fn stringTrim(calling_heap: *Heap, str: Handle, trim_chars: Handle) !Handle {
    const bytes = try Heap.getString(str);
    const trim_chars_bytes = try Heap.getString(trim_chars);

    const start = stringutil.trimLeft(bytes, trim_chars_bytes);
    const end = stringutil.trimRight(bytes, trim_chars_bytes);

    if (start == 0 and end == bytes.len) {
        return str;
    } else {
        return try newString(calling_heap, bytes[start..end]);
    }
}

//////////////////////////////
//  Enum related functions  //

/// Byte count of enum names joined by ", "
fn enumNamesCount(comptime T: type) usize {
    comptime {
        var result_size = 0;
        for (std.meta.fields(T)) |field| {
            result_size += field.name.len;
        }
        // Be sure to account for ", "
        result_size += ((std.meta.fields(T).len) -| 1) * 2;

        return result_size;
    }
}

/// Enum names joined by ", "
pub inline fn enumNames(comptime T: type) *const [enumNamesCount(T):0]u8 {
    comptime {
        // Fill the buffer
        var buf: [enumNamesCount(T):0]u8 = undefined;
        var w: Io.Writer = .fixed(&buf);

        var first_time = true;
        for (std.meta.fields(T)) |field| {
            if (!first_time) {
                w.writeAll(", ") catch unreachable;
            } else first_time = false;

            w.writeAll(field.name) catch unreachable;
        }

        buf[buf.len] = 0;

        const final = buf;
        return &final;
    }
}

pub fn EnumMapping(comptime T: type) type {
    comptime {
        const field_count = std.meta.fields(T).len;

        // Create an entry type (instantiated as .{ "foo", .foo })
        const EntryType = std.meta.Tuple(&[_]type{ [:0]const u8, T });
        // Repeat that type for how many fields there are
        const entries = [1]type{EntryType} ** field_count;
        // Create a map type with those repeated entries
        const Mapping = std.meta.Tuple(&entries);

        // Fill out the map
        var mapping: Mapping = undefined;
        for (std.meta.fields(T), 0..) |variant, i| {
            const entry: EntryType = .{ variant.name, @enumFromInt(variant.value) };
            @field(mapping, std.fmt.comptimePrint("{}", .{i})) = entry;
        }

        // Create the table
        return struct {
            pub const StaticStringMap = std.StaticStringMap(T);

            map: StaticStringMap = StaticStringMap.initComptime(mapping),
        };
    }
}

pub fn TclEnum(comptime T: type, enum_name: []const u8) type {
    return struct {
        pub const variants = T;
        pub const map = (EnumMapping(T){}).map;
        pub const names = enumNames(T);

        pub fn get(calling_heap: *Heap, det: ?*ErrorDetails, value: *Handle) !T {
            const bytes = try value.getString();
            const variant = map.get(bytes);
            if (variant) |unwrapped| {
                return unwrapped;
            } else {
                if (det) |details| details.* = .{
                    .message = try newStringFmt(
                        calling_heap,
                        "bad {s} \"{f}\": must be {s}",
                        .{ enum_name, value.*, names },
                    ),
                };

                return error.BadEnumVariant;
            }
        }
    };
}

test "Tcl enum" {
    const Things = enum { foo, bar, baz };
    const map = (EnumMapping(Things){}).map;
    const names = enumNames(Things);
    try testing.expectEqual(Things.foo, map.get("foo"));
    try testing.expectEqualSlices(u8, "foo, bar, baz", names);
}

/// Runs a string check based on requested class.
pub fn stringIs(calling_heap: *Heap, det: ?*ErrorDetails, str: *Handle, class_to_check: *Handle, strict: bool) !bool {
    const Class = TclEnum(enum {
        integer,
        alpha,
        alnum,
        ascii,
        digit,
        double,
        lower,
        upper,
        space,
        xdigit,
        control,
        print,
        graph,
        punct,
        boolean,
    }, "class");

    const class = try Class.get(calling_heap, det, class_to_check);

    const bytes = try str.getString();
    if (bytes.len == 0) {
        return !strict;
    }

    switch (class) {
        .integer => {
            _ = std.fmt.parseInt(i64, bytes, 0) catch return false;
            return true;
        },
        .double => {
            _ = std.fmt.parseFloat(f64, bytes) catch return false;
            return true;
        },
        .boolean => {
            _ = getBoolean(calling_heap, null, str) catch return false;
            return true;
        },
        .alpha => return stringutil.checkAllAscii(bytes, std.ascii.isAlphabetic),
        .alnum => return stringutil.checkAllAscii(bytes, std.ascii.isAlphanumeric),
        .ascii => return stringutil.checkAllAscii(bytes, std.ascii.isAscii),
        .digit => return stringutil.checkAllAscii(bytes, std.ascii.isDigit),
        .lower => return stringutil.checkAllAscii(bytes, std.ascii.isLower),
        .upper => return stringutil.checkAllAscii(bytes, std.ascii.isUpper),
        .space => return stringutil.checkAllAscii(bytes, std.ascii.isWhitespace),
        .xdigit => return stringutil.checkAllAscii(bytes, stringutil.isHexDigit),
        .control => return stringutil.checkAllAscii(bytes, std.ascii.isControl),
        .print => return stringutil.checkAllAscii(bytes, std.ascii.isPrint),
        .graph => return stringutil.checkAllAscii(bytes, stringutil.isGraph),
        .punct => return stringutil.checkAllAscii(bytes, stringutil.isPunct),
    }
}

test "String is" {
    const ta = testing.allocator;
    const heap = try Heap.createHeap(ta);
    defer Heap.deinitAll();

    var str = try newString(heap, "abcdefg");
    var str2 = try newString(heap, "abcdefg123");
    var class = try newString(heap, "alpha");
    var bad_class = try newString(heap, "bad_class");
    var details: ErrorDetails = undefined;

    try testing.expectEqual(true, try stringIs(heap, &details, &str, &class, false));
    try testing.expectEqual(false, try stringIs(heap, &details, &str2, &class, false));
    try testing.expectError(error.BadEnumVariant, stringIs(heap, &details, &str, &bad_class, false));
    try testing.expectEqualStrings(
        "bad class \"bad_class\": must be integer, alpha, alnum, ascii, digit, " ++
            "double, lower, upper, space, xdigit, control, print, graph, punct, boolean",
        try details.message.getString(),
    );
}

pub fn convertParserError(heap: *Heap, err: Parser.Error) error{OutOfMemory}!ErrorDetails {
    switch (err) {
        error.CharactersAfterCloseBrace => {
            return .{ .message = try newString(heap, "extra characters after close-brace") };
        },
        error.MissingCloseBrace => {
            return .{ .message = try newString(heap, "missing close-brace") };
        },
        error.MissingCloseBracket => {
            return .{ .message = try newString(heap, "unmatched \"[\"") };
        },
        error.MissingCloseQuote => {
            return .{ .message = try newString(heap, "missing quote") };
        },
        error.TrailingBackslash => {
            return .{ .message = try newString(heap, "no character after \\") };
        },
        error.NotVariable => unreachable,
    }
}

pub fn newUninitializedList(heap: *Heap, len: u32) !Handle {
    // `1 +` to make space for the list's head
    const list_index = try heap.createObjects(1 + len);
    const list: []Heap.Object = heap.objects.items(.object)[list_index..][0..(len + 1)];

    list[0] = .{
        .str = Heap.Object.null_string,
        .tag = .list,
        .body = .{
            .list = .{
                .len = len,
            },
        },
    };

    return heap.normalHandle(list_index);
}

pub fn newList(heap: *Heap, handles: []const Handle) !Handle {
    const list = try newUninitializedList(heap, @intCast(handles.len));
    errdefer heap.release(list);

    const new_items = listItemsRaw(list);

    for (handles, new_items) |handle, *item| {
        item.* = try heap.duplicateOrReference(handle);
    }

    return list;
}

/// If shimmering, it creates the object in the calling heap
pub fn shimmerToList(calling_heap: *Heap, det: ?*ErrorDetails, handle: *Handle) !void {
    if (handle.peek().tag == .list) return;

    const obj = handle.peek();
    const obj_heap = handle.getHeap();

    // Optimise dict -> list for object with no string rep.
    if (obj.tag == .dict and Heap.getStringDetails(handle.*) == .null) {
        try Heap.ensureShimmerable(calling_heap, handle);

        const dict = &obj_heap.dicts.items[obj.body.dict];
        const len = dict.len;

        // Discard the old hash map.
        dict.dict.clearAndFree(obj_heap.gpa);

        // Because both lists and dicts store their values directly after,
        // we can just swap out the head to convert to a list.
        obj.* = .{
            .str = Heap.Object.null_string,
            .tag = .list,
            .body = .{
                .list = .{ .len = len },
            },
        };
    } else {
        // No need to duplicate the handle if it can't shimmer, we have to create
        // a new object anyways.

        // Try to preserve information about filename / line number.
        var sourceInfo: ?SourceInfo = null;
        defer if (sourceInfo) |*info| info.deinit(calling_heap.gpa);
        if (getSourceInfo(handle.*)) |info| {
            // TODO PERF see if it's possible to not duplicate the filename here
            sourceInfo = try info.duplicate(calling_heap.gpa);
        }

        const str = try handle.getString();
        var parser = Parser.init(str);

        var arena_instance = std.heap.ArenaAllocator.init(calling_heap.gpa);
        defer arena_instance.deinit();
        const arena = arena_instance.allocator();

        // Figure out how many tokens there are, so we can create the correct list size
        // in the heap.
        var tokens: std.ArrayList(Parser.Token) = .{};
        while (true) {
            const next_token = parser.parseList() catch |e| {
                if (det) |details| details.* = try convertParserError(calling_heap, e);
                return e;
            };
            switch (next_token.tag) {
                .simple_string, .escaped_string => {
                    try tokens.append(arena, next_token);
                },
                .end_of_file => break,
                else => {
                    // Skip any line breaks or word breaks.
                },
            }
        }

        const new_list = try newUninitializedList(calling_heap, @intCast(tokens.items.len));
        errdefer calling_heap.release(new_list);

        for (tokens.items, 0..) |token, i| {
            const item_idx: u32 = @intCast(new_list.index + 1 + i);

            if (token.tag == .simple_string) {
                // Normal string, so no escaping needed.
                const did_set = try calling_heap.setNormalString(item_idx, str[token.loc.start..token.loc.end]);
                if (!did_set) {
                    // Need to duplicate the string, because the heap may take ownership
                    const token_str = try calling_heap.gpa.allocSentinel(u8, token.loc.end - token.loc.start, 0);
                    errdefer calling_heap.gpa.free(token_str);
                    @memcpy(token_str, str[token.loc.start..token.loc.end]);

                    const did_take = try calling_heap.setLongString(item_idx, token_str, .{
                        .different_capacity = str.len,
                    });
                    if (!did_take) calling_heap.gpa.free(token_str);
                }
            } else {
                // Needs escaping. We'll create another string to copy the escaped string into.
                const escaped_str = try calling_heap.gpa.allocSentinel(u8, token.loc.end - token.loc.start, 0);
                errdefer calling_heap.gpa.free(escaped_str);
                const written = stringutil.removeEscaping(str[token.loc.start..token.loc.end], escaped_str);
                escaped_str[written] = 0; // null terminator

                const did_set = try calling_heap.setNormalString(item_idx, escaped_str[0..written]);
                if (!did_set) {
                    // Too large for normal string, so we'll try setting as a long string
                    const did_take = try calling_heap.setLongString(
                        item_idx,
                        escaped_str[0..written :0],
                        .{ .different_capacity = escaped_str.len },
                    );
                    if (!did_take) calling_heap.gpa.free(escaped_str);
                }
            }
        }

        const old_handle = handle.*;
        handle.* = new_list;
        calling_heap.release(old_handle);
    }
}

/// Panics if provided handle is not a list.
fn listSetLength(calling_heap: *Heap, handle: *Handle, new_len: u32) !void {
    const list = handle.*.peek();
    assert(list.tag == .list);

    // No need to resize if there's enough space.
    if (new_len <= list.body.list.len) {
        list.body.list.len = new_len;
        return;
    }

    // Even if there's not enough length, there may be enough capacity.
    const order = handle.getHeap().objects.items(.metadata)[handle.index].order;
    const capacity = memutil.getOrderSize(order) - 1; // -1 for list head
    if (new_len <= capacity) {
        list.body.list.len = new_len;
    }

    // We've exhausted all other options, so we'll need to make a new list.
    const new_list = try newUninitializedList(calling_heap, new_len);
    errdefer calling_heap.freeObject(new_list);
    const new_items = listItemsRaw(new_list);

    if (handle.isShared()) {
        // If the list is shared, we need to duplicate all the items.
        for (0.., new_items) |i, *new_item| {
            new_item.* = try Heap.duplicateOrReference(calling_heap, listItemRaw(handle.*, @intCast(i)));
        }

        const old_handle = handle.*;
        handle.* = new_list;
        calling_heap.release(old_handle);
    } else {
        // If the list isn't shared, we can copy over the objects over without
        // any duplication.
        const old_items = listItemsRaw(handle.*);
        for (old_items, new_items) |old_item, *new_item| {
            new_item.* = old_item;
        }

        // Free the old list without running destructors (because the objects are
        // still in use).
        handle.getHeap().freeObjectBacking(handle.*);

        handle.* = new_list;
    }
}

/// Assumes provided handle is a list.
fn listItemRaw(handle: Handle, index: u32) Handle {
    const list = handle.peek();
    assert(list.tag == .list);

    if (index < list.body.list.len) {
        return .{
            .index = handle.index + 1 + index,
            .heap = handle.heap,
            .ref_counted = false,
        };
    } else @panic("List element out of bounds");
}

pub fn listItem(calling_heap: *Heap, det: *ErrorDetails, handle: *Handle, index: u32) !?Handle {
    try shimmerToList(calling_heap, det, handle);
    const list = handle.peek().body.list;

    if (index < list.len) {
        return listItemRaw(handle.*, index);
    } else return null;
}

/// Assumes handle is a list.
fn listItemsRaw(handle: Handle) []Heap.Object {
    const list = handle.peek();
    assert(list.tag == .list);

    return handle.getHeap().objects.items(.object)[(handle.index + 1)..][0..list.body.list.len];
}

pub fn listAppend(calling_heap: *Heap, det: ?*ErrorDetails, handle: *Handle, item: Handle) !void {
    try shimmerToList(calling_heap, det, handle);
    try listSetLength(calling_heap, handle, handle.peek().body.list.len + 1);

    const list = handle.peek();
    const index = list.body.list.len - 1;

    listItemsRaw(handle.*)[index] = try calling_heap.duplicateOrReference(item);
}

test "Lists" {
    const ta = testing.allocator;
    const heap = try Heap.createHeap(ta);
    defer Heap.deinitAll();
    var det: ErrorDetails = undefined;

    // Simple case: two objects in a list
    const obj1 = try newString(heap, "object 1");
    const obj2 = try newString(heap, "object 2");
    var list1 = try newList(heap, &.{ obj1, obj2 });

    const items = listItemsRaw(list1);
    try testing.expectEqual(2, items.len);
    // The object should have been copied when being moved into the list
    try testing.expect(obj1.peek().str != items[0].str);
    // But it should have an identical string
    try testing.expectEqualStrings("object 1", try listItemRaw(list1, 0).getString());

    const to_append = try newString(heap, "appended item");
    try listAppend(heap, &det, &list1, to_append);
    try testing.expectEqualStrings("appended item", try listItemRaw(list1, 2).getString());

    var string_list = try newString(heap,
        \\item1 {item 2} item\ 3
    );
    // const old_string_list_handle = string_list;
    try shimmerToList(heap, &det, &string_list);
    // try testing.expect(old_string_list_handle != string_list);
    // try testing.expectEqualStrings("item1", try listItemRaw(string_list, 0).getString());
    // try testing.expectEqualStrings("item 2", try listItemRaw(string_list, 1).getString());
    // try testing.expectEqualStrings("item 3", try listItemRaw(string_list, 2).getString());
}

pub const SourceInfo = struct {
    filename_ref: [:0]const u8,
    line_no: u32,

    pub fn duplicate(self: *const SourceInfo, gpa: std.mem.Allocator) !SourceInfo {
        return .{
            .filename_ref = try gpa.dupeZ(u8, self.filename_ref),
            .line_no = self.line_no,
        };
    }

    pub fn deinit(self: *SourceInfo, gpa: std.mem.Allocator) void {
        gpa.free(self.filename_ref);
    }
};

pub fn getSourceInfo(handle: Handle) ?SourceInfo {
    const ref = handle.peek();

    if (ref.tag != .source) return null;

    return .{
        .filename_ref = handle.getHeap().getHeapStringZ(ref.body.source.file_name),
        .line_no = ref.body.source.line_no,
    };
}

/// Will return error.EmbeddedNull if `filename` has embedded nulls.
pub fn setSourceInfo(calling_heap: *Heap, obj: Handle, file_name: [:0]const u8, line_no: u32) !void {
    for (file_name) |byte| {
        if (byte == 0) return error.EmbeddedNull;
    }

    Heap.invalidateBody(calling_heap, obj);

    // Allocate space for the filename string (has to be in the heap, because
    // the string handles are stored as a u32 in the source rep)
    const len: u32 = @intCast(file_name.len);
    const filename_in_heap = try calling_heap.createString(len);
    errdefer calling_heap.freeString(filename_in_heap, len);

    // Copy the filename
    @memcpy(calling_heap.getHeapString(filename_in_heap, filename_in_heap + len), file_name);

    const ref = obj.peek();
    ref.tag = .source;
    ref.body.source.file_name = filename_in_heap;
    ref.body.source.line_no = line_no;
}

test "Source info" {
    const ta = testing.allocator;
    const heap = try Heap.createHeap(ta);
    defer Heap.deinitAll();

    const obj = try heap.createObject();
    try setSourceInfo(heap, obj, "test_file.tcl", 42);

    // Verify the object has the source tag
    const ref = obj.peek();
    try testing.expectEqual(.source, ref.tag);
    try testing.expectEqual(@as(u32, 42), ref.body.source.line_no);

    const info = getSourceInfo(obj);
    if (info) |unwrapped| {
        try testing.expectEqualSlices(u8, "test_file.tcl", unwrapped.filename_ref);
        try testing.expectEqual(@as(u32, 42), unwrapped.line_no);
    } else return error.TestUnexpectedResult;

    const obj2 = try newString(heap, "hello");
    const empty_info = getSourceInfo(obj2);
    try testing.expect(empty_info == null);
}

var next_script_id = 1;

////////////////////////////////
//  Script related functions  //

/// Not threadsafe.
pub fn shimmerToScript(calling_heap: *Heap, det: ?*ErrorDetails, handle: Handle) !void {
    try Heap.ensureShimmerable(calling_heap, handle);

    const bytes = try handle.getString();
    var parser = Parser.init(bytes);

    // Parse all the tokens of the script, handling any errors that come up.

    // Set up tokens list.
    var tokens = try std.ArrayList(Parser.Token.Tag).initCapacity(calling_heap.gpa, bytes.len / 8);
    errdefer tokens.deinit(calling_heap.gpa);

    // Track how many commands there are, so we can be sure to make a .start_of_line for
    // each of them.
    var command_count: usize = 1;
    // Also track how many words there are that have multiple components (i.e. something like `foo$bar`)
    // so we have enough space for all needed ".start_of_word"s.
    var multi_word_count: usize = 0;
    // Count how many sections are within this word ("foo", 1 section, "foo$bar", 2 sections, etc).
    var word_section_count: usize = 0;
    // How many separators were found (we'll subtract one from the total for each separator).
    var separator_count: usize = 0;
    // Used for trimming the first whitespace, if any.
    var is_first_token: bool = true;
    // Add all tokens to the list, handling any errors that may come up.
    while (true) {
        const next_token = parser.parseScript();
        if (next_token) |token| {
            tokens.append(calling_heap.gpa, token);
            switch (token.tag) {
                .word_separator => {
                    separator_count += 1;
                    word_section_count = 0;
                },
                .command_separator => {
                    separator_count += 1;
                    // Make sure we don't double-count the first command.
                    if (!is_first_token) command_count += 1;
                },
                .argument_expansion => {
                    // Argument expansion is always considered a multi word, though
                    // it inserts an .argument_expansion token instead of a .start_or_word
                    // token.
                    multi_word_count += 1;
                    word_section_count = 0;
                },
                .simple_string, .escaped_string, .variable_subst, .dict_sugar, .command_subst => {
                    word_section_count += 1;
                    // Why exactly 2? Because otherwise multi_word_count would increment for every
                    // section of a multi word.
                    if (word_section_count == 2) multi_word_count += 1;
                },
                .end_of_file => break,
            }
        } else |err| {
            if (det) |details| {
                details.* = try convertParserError(calling_heap, err);
                if (parser.error_details) |parser_details| {
                    details.index = parser_details.index;
                }
            }
            return err;
        }

        is_first_token = false;
    }

    const new_token_count = tokens.len + command_count + multi_word_count - separator_count;

    // Initialize the Heap-stored list that will store all the corrisponding value for each token
    const token_values = try newUninitializedList(calling_heap, new_token_count);
    errdefer calling_heap.release(token_values);
    const converted_tokens = try calling_heap.gpa.alloc(Parser.Token.Tag, new_token_count);
    errdefer calling_heap.gpa.free(converted_tokens);
    // const elements = listElements(calling_heap, null, token_values) catch unreachable;

    // // The initial command separator was trimmed, if it existed in the first place,
    // // so we'll start off with it.
    // var token: Parser.Token.Tag = .command_separator;
    // // Where the command started (the start_of_line token), so we can edit its information
    // // when we reach the end of the command.
    // var command_start: usize = 0;
    // var i: usize = 0;

    // while (i < )
    // for (tokens.items, 0..) |token, i| {
    //     if (token[i] == .command_separator) {
    //         converted_tokens[i] = .start_of_line;
    //         elements[i].tag = .script_line;
    //     }
    // }
}

pub fn shimmerToBoolean(calling_heap: *Heap, det: ?*ErrorDetails, handle: *Handle) !void {
    try Heap.ensureShimmerable(calling_heap, handle);

    const Mapping = std.StaticStringMap(bool).initComptime(.{
        .{ "1", true },  .{ "true", true },   .{ "yes", true }, .{ "on", true },
        .{ "0", false }, .{ "false", false }, .{ "no", false }, .{ "off", false },
    });

    const bytes = try handle.getString();
    const new_value = Mapping.get(bytes) orelse {
        if (det) |details| details.* = .{
            .message = try newStringFmt(
                calling_heap,
                "expected boolean but got \"{f}\"",
                .{handle},
            ),
        };
        return error.BadBoolean;
    };

    const ref = handle.peek();
    ref.tag = .bool;
    ref.body.bool = new_value;
}

pub fn getBoolean(calling_heap: *Heap, det: ?*ErrorDetails, handle: *Handle) !bool {
    try shimmerToBoolean(calling_heap, det, handle);

    return handle.peek().body.bool;
}
